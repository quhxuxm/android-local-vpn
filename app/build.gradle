plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
}

android {
    namespace 'com.ppaass.agent'
    compileSdk 33
    ndkVersion '25.2.9519653'
    defaultConfig {
        applicationId "com.ppaass.agent"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lint {
        disable 'PackagedPrivateKey'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }

    packagingOptions {
        doNotStrip "**/libvpn.so"
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
    module = "../rust"       // Or whatever directory contains your Cargo.toml
    libname = "vpn"          // Or whatever matches Cargo.toml's [package] name.
    targets = ['arm64', 'x86_64']      // See bellow for a longer list of options
    verbose = true
    profile = 'release'
    features {
        all()
    }
}

tasks.configureEach { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation "androidx.compose.ui:ui:1.4.3"
    implementation "androidx.compose.material:material:1.4.3"
    implementation "androidx.compose.ui:ui-tooling-preview:1.4.3"
    implementation "androidx.compose.runtime:runtime-livedata:1.4.3"
    implementation "com.google.accompanist:accompanist-pager:0.25.0"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.25.0"
    implementation 'org.jsoup:jsoup:1.15.2'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'dnsjava:dnsjava:3.5.1'
    debugImplementation "androidx.compose.ui:ui-tooling:1.4.3"
}